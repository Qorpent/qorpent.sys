# Copyright Comdiv 2007-2013
# qorpent.sys bxl#

###############################################
# ДАННЫЙ ПРИМЕР ПОКАЗЫАЕТ                     #
# ПРИМЕР БАЗИСА СИСТЕМА А-ЛЯ ECO              #
# режим работы с тестом                       #
# UseInterpolation = true                     #
# BxlSharp = true                             #
###############################################

#  Здесь показан пример возможного исполнения абстракции
#  классических тем АССОИ с учетом новых веяний и языка
#  на что обратить внимание
#    а) замена "генерик-тем" subeco на простое определение элементов
#    б) упрощенная прошивка "умолчаний" в имена отчетов и прочего без перегрузки абстракции лишними свойствами"
#    в) использование условной компиляции вместо вырожденного атрибута activeA и т.п.
#    г) на то, что предполагается, что такие вещи как rootrow и т.п. система сама в состоянии высчитать по теме-
#       нет необходимости сложно прописывать их в сами form/report
#    д) на то, что конечные рабочие темы крайне упрощены и по сути управляются флагами
#    e) предполагается, что колсеты и наборы параметров хранятся отельно и привязываются по ИМЕНИ причем на уровне темы 
#      что по идее должно вытеснить БИБЛИОТЕКИ из старой версии

# во-первых определяем, что у нас вообще в системе есть темы
# используем атрибут prototype просто как метку для этого класса и всех наследников
class thema abstract prototype=thema
	element form
	element report

thema eco abstract
	_form = true
	_report = true
	_obj = true
	_svod = true
	_fact = true
	_plan = true
	_korr = false
	
	_readrole = '${roleprefix}_READ'
	_svodrole = '${roleprefix}_ANALYTIC'
	_writerole = '${roleprefix}_WRITE'
	
	role = '${_readrole}'
	
	form A role='${_writerole}' if='_form & _fact'
	form B role='${_writerole}' if='_form & _plan'
	form C role='${_writerole}' if='_form & _korr'
	report Aa role='${_svodrole}' if='_report & _fact & _svod'
	report Ab role='${_readrole}' if='_report & _fact & _obj'
	report Ba role='${_svodrole}' if='_report & _plan & _svod'
	report Bb role='${_readrole}' if='_report & _plan & _obj'
	
	report Ca role='${_svodrole}' if='_report & _korr & _svod'
	report Cb role='${_readrole}' if='_report & _korr & _obj'
	
	paramset ps_main if='_svod & !_remove_ps_main'

eco month 'Типовая месячная тема' abstract
	colset cs_month_base for=all if='!_remove_cs_month_base'
	colset cs_month_plan for='form B,report Ba, report Bb'  if='_plan & !_remove_cs_month_plan'
	
	
month balans rootrow=m111 roleprefix=FIN

balans balans_rsn _korr=true
	+report Bb
		col mycol2
		
balans balans_rsn2 _korr=true
	~report Bb
		col mycol2
		
balans balans_olap _form=false _plan=false _obj=false
	_remove_cs_month_base = true
	colset cs_month_olap

	
	

	
	

