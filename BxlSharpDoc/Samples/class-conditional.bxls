# Copyright Comdiv 2007-2013
# qorpent.sys bxl#

###############################################
# ДАННЫЙ ПРИМЕР ПОКАЗЫАЕТ                     #
# БАЗОВУЮ РАБОТУ УСЛОВНОЙ КОМПИЛЯЦИЕЙ		  #
# режим работы с тестом                       #
# Interpolation = true                        #
# BxlSharp = true                             #
###############################################

# часто оказывается удобным сделать базоый класс (по сути шаблон) на все основные случаи жизни,
# но при этом, чтобы был простой способ включать или выключать опции
# для этого используется механизм условной компиляции посредством атрибута "if"
# "if" может присутсвовать в составле любого элемента класса, включая "import"

# определим класс обед с меню (это типа абстракция обеда)
class Dinner Обед abstract 
	sup sol Солянка if='первое & !вегетариан'
	sup okr Окрошка if='первое & вегетариан'
	main salat Салат if='второе'
	main kotl Котлета if='второе & !вегетариан' 
	main kart Картошка if='второе & !худеть' 
	des sok Сок if='!праздник | вегетариан'
	des pivo Пиво if='праздник & !вегетариан'
	des kivi Киви if='праздник & вегетариан'
	des bulka Булочка if='десерт & !худеть'
	

Dinner Girl_Every_Day "Ежедневный обед Девушки" вегетариан первое второе худеть
Dinner Boy_Every_Day "Ежедневный обед Парня"  первое второе десерт


Dinner Girl_Holiday "Обед Девушки по праздникам" праздник вегетариан первое второе десерт 
Dinner Boy_Holiday "Ежедневный обед Парня"  праздник первое второе десерт 
	

	
	

